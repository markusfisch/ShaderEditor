apply plugin: 'com.android.application'
apply plugin: 'findbugs'

android {
	compileSdkVersion sdk_version
	buildToolsVersion build_tools_version

	defaultConfig {
		minSdkVersion 9
		targetSdkVersion sdk_version

		versionCode 51
		versionName '2.16.0'
	}

	signingConfigs {
		config {
			keyAlias System.getenv('ANDROID_KEY_ALIAS')
			keyPassword System.getenv('ANDROID_KEY_PASSWORD')
			storePassword System.getenv('ANDROID_STORE_PASSWORD')
			def filePath = System.getenv('ANDROID_KEYFILE')
			storeFile filePath ? file(filePath) : null
		}
	}

	buildTypes {
		debug {
			applicationIdSuffix '.debug'
		}

		release {
			minifyEnabled true
			shrinkResources true
			signingConfig signingConfigs.config
		}
	}
}

dependencies {
	implementation "com.android.support:appcompat-v7:$support_version"
	implementation "com.android.support:design:$support_version"
	implementation "com.android.support:preference-v7:$support_version"
}

task findbugs(type: FindBugs, dependsOn: 'assembleDebug') {
	excludeFilter = file('./findbugs-exclude.xml')
	source = fileTree('src/main/java/')
	classes = files("${project.rootDir}/app/build/intermediates/javac")
	classpath = files()

	effort = 'default'

	reports {
		xml.enabled = false
		html.enabled = true
	}
}
